# Spicy grammar for the IGMP protocol.
# Supports IGMPv1, v2 and v3.
# See RFC 1112 (IGMPv1), RFC 2236 (IGMPv2) and RFC 3376 (IGMPv3)
# for information about the IGMP protocol.

module IGMP;

# ICMP message types
type Type = enum {
    MQ =   0x11,
    MRV1 = 0x12,
    MRV2 = 0x16,
    LG =   0x17,
    MRV3 = 0x22
};

# Version 3 Membership Report Group Record Types
type GroupType = enum {
    MODE_IS_INCLUDE =        1,
    MODE_IS_EXCLUDE =        2,
    CHANGE_TO_INCLUDE_MODE = 3,
    CHANGE_TO_EXCLUDE_MODE = 4,
    ALLOW_NEW_SOURCES =      5,
    BLOCK_OLD_SOURCES =      6
};

# Generic IGMP message
public type Message = unit {
    msg_type:      uint8 &convert=Type($$);

    # Determine message type
    switch ( self.msg_type ) {
        Type::MQ   -> mq:   MembershipQuery;
        Type::MRV1 -> mrv1: MembershipReportV1;
        Type::MRV2 -> mrv2: MessageV2;
        Type::LG   -> lg:   MessageV2;
        Type::MRV3 -> mrv3: MembershipReportV3;
    };

    on %done { print self; }
};

# Generic IGMPv2 message
type MessageV2 = unit {
    max_resp_time: uint8;
    checksum:      uint16;
    group_addr:    addr &ipv4;
};

# msg_type = 0x11: Membership Query
type MembershipQuery = unit {
    max_resp_code:   uint8;
    checksum:        uint16;
    group_addr:      addr &ipv4;
    flags: bitfield(8) {
        resv: 0..3;
        s:    4;
        qrv:  5..7;
    };
    qqic:            uint8;
    num_sources:     uint16;
    sources:         addr[self.num_sources] &ipv4;
    additional_data: bytes &eod;
};

# msg_type = 0x12: Version 1 Membership Report
type MembershipReportV1 = unit {
    unused:     uint8;
    checksum:   uint16;
    group_addr: addr &ipv4;
};

# Version 3 Group Record
type Group = unit {
    group_type:     uint8 &convert=GroupType($$);
    aux_data_len:   uint8;
    num_sources:    uint16;
    multicast_addr: addr &ipv4;
    sources:        addr[self.num_sources] &ipv4;
    aux_data:       bytes &eod;
};

# msg_type = 0x22: Version 3 Membership Report
type MembershipReportV3 = unit {
    resv1:      uint8;
    checksum:   uint16;
    resv2:      uint16;
    num_groups: uint16;
    groups:     Group[self.num_groups];
};
